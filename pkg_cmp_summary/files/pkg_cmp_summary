#!/usr/bin/awk -f

# Copyright (c) 2007-2008 Aleksey Cheusov <vle@gmx.net>
#
# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
# LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
# OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

############################################################
# dewey function
BEGIN {
	__dewey_maxf = 9 # 8(YYYYMMDD) + 1
	for (i=0; i <= 26; ++i){
		# I don't know anything about EBCDIC :-P :-)
		char2dotver [sprintf("%c", 97 + i)] = "." i+1
	}
}

function chars (n, c,            s){
	s = ""

	while (n-- > 0){
		s = s c
	}

	return s
}

function dewey2str (ver,        left,right,sym,num,last){
	ver = "." ver

	gsub(/alpha/, "A", ver)
	gsub(/beta/, "B", ver)
	gsub(/rc/, "C", ver)
	gsub(/pre/, "C", ver)
	gsub(/nb/, "Z", ver)
	gsub(/pl/, ".", ver)

	if (ver ~ /[qwertyuiopasdfghjklzxcvbnm]$/){
		last = substr(ver, length(ver), 1)
		ver = substr(ver, 1, length(ver)-1) char2dotver [last]
	}

	gsub(/[.]/, "Y" chars(__dewey_maxf, " ") ".", ver)

	while (match(ver, /[ABCZ.][0-9]+/)){
		left  = substr(ver, 1, RSTART-1)
		sym   = substr(ver, RSTART, 1)
		num   = substr(ver, RSTART+1, RLENGTH-1)
		right = substr(ver, RSTART+RLENGTH)

		num = sprintf("%" __dewey_maxf "s", num)

		if (sym == ".")
			ver = left "Y" num right
		else
			ver = left sym num right
	}

	return substr(ver "Y", __dewey_maxf + 2)
}

function cmp_dewey (ver1, ver2,             s1,s2){
	if (ver1 == ver2){
		return "="
	}

	s1 = dewey2str(ver1)
	s2 = dewey2str(ver2)

	if (s1 < s2)
		return "<"
	else if (s1 > s2)
		return ">"
	else
		return "="
}

############################################################
# file reading
BEGIN {
	last_fn    = ""
	first_file = 1
}

function trim (s){
	sub(/^[ \t]+/, "", s)
	sub(/[ \t]+$/, "", s)

	return s
}

last_fn != FILENAME {
	if (last_fn != ""){
		first_file = 0
	}

	last_fn = FILENAME
}

$0 ~ /^PKGNAME=/ {
	pkgname = trim(substr($0, 9))

	# ver
	ver = pkgname
	sub(/^.*-/, "", ver)

	# pkgbase
	pkgbase = pkgname
	sub(/-[^-]+$/, "", pkgbase)

	#
	if (first_file){
		# first file!
		if (pkgbase in names){
			if (! (pkgbase in duplicates))
				duplicates [pkgbase] = 1

			++duplicates [pkgbase]
		}else{
			names [pkgbase] = ver
		}
	}else{
		present [pkgbase] = 0

		# second file!
		if (pkgbase in duplicates){
			next
		}

		if (! (pkgbase in names)){
			print "+", pkgbase, ver
			next
		}

		ver1 = names [pkgbase]
		res = cmp_dewey(ver1, ver)
		print res, pkgbase, ver1, ver
	}

	next
}

END {
	for (pkgbase in names){
		if (! (pkgbase in present)){
			print "-", pkgbase, names [pkgbase]
			delete duplicates [pkgbase]
		}else if (pkgbase in duplicates){
			print duplicates [pkgbase], pkgbase
		}
	}
}
